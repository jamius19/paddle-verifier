plugins {
    id 'jacoco'
    id 'java'
    id 'maven-publish'
    id 'org.jreleaser' version '1.15.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.jamiussiam'
            artifactId = 'paddle-verifier'

            from components.java

            pom {
                name = 'paddle-verifier'
                description = 'A lighweight Paddle.com webhook verification library for Java.'
                url = 'https://github.com/jamius19/paddle-verifier'
                inceptionYear = '2020'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'jamius19'
                        name = 'Jamius Siam'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jamius19/paddle-verifier.git'
                    developerConnection = 'scm:git:ssh://github.com/jamius19/paddle-verifier.git'
                    url = 'https://github.com/jamius19/paddle-verifier'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

group = "com.jamiussiam"
version = "2.2"

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
}

test {
    useJUnitPlatform()

    reports {
        junitXml.required.set(true)
        html.required.set(true)
    }
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(false)
    }
}

tasks.test {
    finalizedBy(tasks.jacocoTestReport)
}